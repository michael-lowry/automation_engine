:PRINT "--- BEGIN UC4.RESOLVE_AGENT_GROUP.JOBI ---"
:SET &AG_AGENT# = ""
:SET &PreProc_Error# = "No error"
:SET &Run_PostProc_on_Error# = &Run_PostProc_on_Error#
:SET &Stop_on_Error# = &Stop_on_Error#
:SET &Stop_Message_Num# = &Stop_Message_Num#
:IF &Stop_Message_Num# = ""
:  SET &Stop_Message_Num# = "0"
:ENDIF
:IF &AGENT_OR_AGENTGROUP# <> ""
:  IF GET_OH_IDNR(&AGENT_OR_AGENTGROUP#) = 0
:    SET &Message# = "Agent/agent group &AGENT_OR_AGENTGROUP# does not exist."
:    PRINT "ERROR: &Message#"
:    SET &PreProc_Error# = "Agent/group does not exist"
:  ENDIF
:  SWITCH GET_OBJECT_TYPE(&AGENT_OR_AGENTGROUP#)
:  CASE 'HOSTG'
:    PRINT "&AGENT_OR_AGENTGROUP# is an agent group. Checking AG mode..."
:    SET &AGENTGROUP# = &AGENT_OR_AGENTGROUP#
:    SET &AG_Mode# = GET_VAR(UC4.GET_AGENTGROUP_MODE.VARA_SEC_SQLI)
:    SWITCH &AG_Mode#
:    CASE 'A'
:      SET &AG_Mode_Desc# = "any"
:    CASE 'F'
:      SET &AG_Mode_Desc# = "first"
:    CASE 'N'
:      SET &AG_Mode_Desc# = "next"
:    CASE 'L'
:      SET &AG_Mode_Desc# = "load dependent"
:    CASE 'X'
:      SET &AG_Mode_Desc# = "all"
:    ENDSWITCH
:    PRINT "&AGENT_OR_AGENTGROUP# mode is '&AG_Mode_Desc#' (&AG_Mode#)."
:    IF &AG_Mode# = 'X'
:      PRINT "WARNING: Agent group will be resolved to a single specific agent."
:    ENDIF
:    PRINT "Checking agent group members..."
:    SET &AG_HND# = PREP_PROCESS_AGENTGROUP(&AGENT_OR_AGENTGROUP#,,'BY_RULE')
:    PROCESS &AG_HND#
:      SET &AG_AGENT# = GET_PROCESS_LINE(&AG_HND#,1)
:      SET &AGENT_UP# = GET_PROCESS_LINE(&AG_HND#,2)
:      PRINT "Agent chosen by rule from agent group: &AG_AGENT#"
:      IF &AGENT_UP# = 'Y'
:        PRINT "Agent &AG_AGENT# is up."
:        SET &AGENT# = &AG_AGENT#
!:        TERM_PROCESS
:      ELSE
:        SET &Message# = "No active &AGENT_OR_AGENTGROUP# agent!"
:        PRINT "ERROR: &Message#"
:        SET &PreProc_Error# = "No active agent in agent group (1)"
:      ENDIF
:    ENDPROCESS
:    CLOSE_PROCESS &AG_HND#
!    Sometimes, PREP_PROCESS_AGENTGROUP returns an empty data sequence.
:    IF &AG_AGENT# = " "
:      SET &Message# = "No active &AGENT_OR_AGENTGROUP# agent!"
:      PRINT "ERROR: &Message#"
:      SET &PreProc_Error# = "No active agent in agent group (2)"
:    ENDIF
:  CASE 'HOST'
:    PRINT "&AGENT_OR_AGENTGROUP# is an agent."
:    SET &AGENT_UP# = SYS_HOST_ALIVE(&AGENT_OR_AGENTGROUP#)
:    IF &AGENT_UP#  = "Y"
:      PRINT "Agent is up."
:      SET &AGENT# = &AGENT_OR_AGENTGROUP#
:    ELSE
:      SET &Message# = "Agent &AGENT_OR_AGENTGROUP# is down!"
:      PRINT "ERROR: &Message#"
:      SET &PreProc_Error# = "Agent is down."
:    ENDIF
:  OTHER
:    PRINT "&AGENT_OR_AGENTGROUP# is not a known agent or agent group."
:    SET &Message# = "&AGENT_OR_AGENTGROUP# unknown!"
:    PRINT "ERROR: &Message#"
:    SET &PreProc_Error# = "Unknonwn agent/agent group"
:  ENDSWITCH
:ELSE
:  PRINT "No agent or agent group specified in variable &&AGENT_OR_AGENTGROUP#."
:ENDIF
:IF &AGENT# <> ""
:  PRINT "Object name    : &$NAME#"
:  PRINT "Object type    : &$OBJECT_TYPE#"
:  SET &OBJ_SUBTYPE# = GET_VAR(UC4.GET_OBJ_SUBTYPE.VARA_SEC_SQLI)
:  PRINT "Object subtype : &OBJ_SUBTYPE#"
:  SET &AGENT_TYPE# = GET_VAR(UC_HOST_JCL_VAR,&AGENT#)
:  PRINT "Agent name     : &AGENT#"
:  PRINT "Agent type     : &AGENT_TYPE#"
:  IF &OBJ_SUBTYPE# <> &AGENT_TYPE#
:    PRINT "Mismatch between job subtype and agent type."
:    SET &Message# = "Cannot switch to agent of different type!"
:    PRINT "ERROR: &Message#"
:    SET &PreProc_Error# = "Job/agent type mismatch"
:  ENDIF
:ENDIF
:RSET &PreProc_Error# = &PreProc_Error#
:PRINT "--- END UC4.RESOLVE_AGENT_GROUP.JOBI ---"
:INCLUDE UC4.ERROR_HANDLING_PREPROC.JOBI
